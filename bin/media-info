#!perl

use 5.010;
use strict;
use warnings;
#use Log::Any '$log';

use Perinci::CmdLine;
use Media::Info qw(get_media_info);

# VERSION

our %SPEC;

$SPEC{media_info} = {
    v => 1.1,
    summary => 'Get information about media files/URLs',
    args => {
        media => {
            summary => 'Media files/URLs',
            schema => ['array*' => of => 'str*'],
            req => 1,
            pos => 0,
            greedy => 1,
        },
    },
};
sub media_info {
    my %args = @_;

    my $media = $args{media};

    if (@$media == 1) {
        return get_media_info(media => $media->[0]);
    } else {
        my @res;
        for (@$media) {
            my $res = get_media_info(media => $_);
            unless ($res->[0] == 200) {
                warn "Can't get media info for '$_': $res->[1] ($res->[0])\n";
                next;
            }
            push @res, { media => $_, %{$res->[2]} };
        }
        [200, "OK", \@res];
    }
}

Perinci::CmdLine->new(url=>'/main/media_info')->run;

1;
# ABSTRACT: Return information about media files/URLs
# PODNAME: media-info

=head1 SYNOPSIS

 % media-info /home/steven/celine.mp4


=head1 DESCRIPTION

This is a command-line interface for the C<get_media_info> function in
L<Media::Info>.

=cut
